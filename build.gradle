buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath "com.badlogicgames.gdx:gdx-jnigen-gradle:2.0.1"
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

apply plugin: "java-library"
apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: "com.badlogicgames.gdx.gdx-jnigen"

dependencies {
    testImplementation "com.badlogicgames.gdx:gdx-jnigen-loader:2.0.1"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
   systemProperty 'file.encoding', 'UTF-8'
}

version = '1.0.2-SNAPSHOT'

jnigen {
    sharedLibName = "demo"
    all {
        headerDirs = ["src"]

        cFlags += " -fvisibility=hidden "
        cppFlags += " -std=c++11 -fvisibility=hidden "
        linkerFlags += " -fvisibility=hidden "
    }
    add(Windows, x32)
    add(Windows, x64)
    add(Linux, x32)
    add(Linux, x64)
    add(Linux, x32, ARM)
    add(Linux, x64, ARM)
    add(MacOsX, x64)
    add(Android)
    add(IOS)
}

task runTest(dependsOn: jnigenJarNativesDesktop, type: JavaExec) {
    classpath = files(jnigenJarNativesDesktop.outputs)
    classpath += sourceSets.test.runtimeClasspath
    main = "demo.JnigenDemoTest"
    args jnigen.sharedLibName
}
test.dependsOn runTest

apply from: 'publish.gradle'